apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'jacoco'

android {
    compileSdkVersion rootProject.compileSdkVersion
    buildToolsVersion rootProject.buildToolsVersion

    defaultConfig {

        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion

    }

    buildTypes {
        debug{
            
        }
        
        qa{
            matchingFallbacks = ['debug']
            testCoverageEnabled = true
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    configurations.all {
        resolutionStrategy {
            resolutionStrategy.force "com.google.guava:guava:27.1-jre"
            resolutionStrategy.force "com.google.code.findbugs:jsr305:3.0.2"

            // fail eagerly on version conflict (includes transitive dependencies)
            // e.g. multiple different versions of the same dependency (group and name are equal)
            failOnVersionConflict()
        }
    }
}

dependencies {

    //Depends on Base Skeletoid
    api project(path: ':base')

    implementation ("com.google.android.gms:play-services-analytics:16.0.5"){
        exclude group: 'androidx.annotation', module: 'annotation'
        exclude group: 'androidx.fragment', module: 'fragment'
        exclude group: 'androidx.core', module: 'core'
        exclude group: 'androidx.legacy', module: 'legacy-support-core-utils'
        exclude group: 'androidx.legacy', module: 'legacy-support-core-ui'
    }
}

jacoco {
    toolVersion "0.8.1"
}

//Unit Test Coverage filtered - Note: This is currently NOT being used on the Codacy report. This is to run locally if needed
task createSkeletoidUnitTestReport(type: JacocoReport, dependsOn: ['testQaUnitTest']) {

    reports {
        html.enabled = true
    }

    def fileFilter = [
            //Android stuff
            '**/R.class',
            '**/BR.class',
            '**/R$*.class',
            '**/BR$*.class',
            '**/BuildConfig.*',
            'android/**/*.*',
            '**/Manifest*.*',
            //Data Binding
            '**/*databinding/**/*.*',
            //Test
            '**/**Test*.*']

    //To support Java coverage on Unit tests
    def debugTree = fileTree(dir: "${buildDir}/intermediates/javac/qa/compileQaJavaWithJavac/classes", excludes: fileFilter)
    //To support Kotlin coverage on Unit tests
    def kotlinDebugTree = fileTree(dir: "${buildDir}/tmp/kotlin-classes/qaUnitTest/", excludes: fileFilter)

    def mainSrc = "${project.projectDir}/src/main/java"
    def debugSrc = "${project.projectDir}/src/debug/java"

    sourceDirectories = files([mainSrc, debugSrc])


    classDirectories = files([debugTree], [kotlinDebugTree])
    executionData = files("${buildDir}/jacoco/testQaUnitTest.exec")
}