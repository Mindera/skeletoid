apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: "jacoco"

android {
    compileSdkVersion rootProject.compileSdkVersion
    buildToolsVersion rootProject.buildToolsVersion

    testBuildType "qa"

    defaultConfig {
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        release.java.srcDirs += 'src/debug/kotlin'
        debug.java.srcDirs += 'src/debug/kotlin'
        qa.java.srcDirs += 'src/qa/kotlin'
    }

    buildTypes {

        debug {
            matchingFallbacks = ['debug']
        }

        qa {
            matchingFallbacks = ['debug']
            testCoverageEnabled = true
        }

        release {
            matchingFallbacks = ['debug']
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {

    // depends on base module
    api project(path: ':base')
    implementation "com.akaita.java:rxjava2-debug:$rxJavaDebug"

    testImplementation project(path: ':base', configuration: 'childTestDependencies')

    androidTestImplementation ("androidx.test:runner:$testRunnerVersion")
    androidTestImplementation("androidx.test.espresso:espresso-core:$espressoVersion")
}

task createUnitTestReport(type: JacocoReport, dependsOn: [':rxjava:testQaUnitTest']) {
    group = "Reporting"
    description = "Generate Jacoco coverage reports after running tests."

    reports {
        html.enabled = true
        xml.enabled = true
    }

    def fileFilter = [
        "**/bindings/**",
        "**/handlers/**",
        "**/operators*.*",
        "**/Any*.*",
        "**/Breadcrumbs*.*",
        "**/Completable*.*",
        "**/DebounceBuffer*.*",
        "**/OnErrorDoAction*.*"
    ]

    def mainSrc = "$projectDir/src/main/java"
    def qaTree = fileTree(dir: "$buildDir/tmp/kotlin-classes/qa", excludes: fileFilter)

    getSourceDirectories().from(files([mainSrc]))
    getClassDirectories().from(files([qaTree]))
    getExecutionData().from(files("$buildDir/jacoco/testQaUnitTest.exec"))
}