apply plugin: "org.sonarqube"

buildscript {
    ext.gradleVersion = '3.2.1'
    ext.kotlinVersion = '1.3.21'

    repositories {
        google()
        jcenter()
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
        maven { url "https://jitpack.io" }
    }
    dependencies {
        classpath "com.android.tools.build:gradle:$gradleVersion"

        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"

        classpath "org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:2.6.2"
    }
}

allprojects {
    repositories {
        google()
        jcenter()
        mavenCentral()
        maven { url "http://dl.bintray.com/typesafe/maven-releases" }
        maven { url "https://maven.google.com" }
        maven { url "https://jitpack.io" }
        apply from: "$rootProject.projectDir/jacoco_global.gradle"
    }
}

ext {
    // sdk and tools
    compileSdkVersion = 28
    buildToolsVersion = '28.0.3'
    minSdkVersion = 19
    targetSdkVersion = 27

    //Support
    supportLibraryVersion = '28.0.0'

    //Play Services
    playServicesVersion = '15.0.1'

    //Google Analytics
    gaVersion = '16.0.5'

    //Firebase
    firebaseVersion = '16.0.4'
    firebaseCommonsVersion = '16.0.1'

    //Rx
    rxJavaVersion = '2.1.7'
    rxAndroidVersion = '2.0.2'
    rxBindingsVersion = '2.1.1'

    //Lint stuff
    httpComponentsVersion = '4.2.5'
    guavaVersion = '22.0'

    //security
    securePreferencesVersion = '1.0.0'
    facebookConcealVersion = '1.1.3@aar'

    //Tests
    junitVersion = '4.12'
    mockitoVersion = '2.8.9'
    powermockVersion = '1.7.4'
    robolectricVersion = '3.8'
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

rootProject.afterEvaluate {
    tasks['sonarqube'].with {
        dependsOn.clear()
        dependsOn 'createGlobalSkeletoidUnitTestReport'
    }
}

subprojects {

    sonarqube {
        properties {
            property "sonar.sources", "src"
            property "sonar.exclusions", "**/*test*/**,build/**,*.iml,**/*generated*,**extensions**"
            property "sonar.tests", "./src/test/"
            property "sonar.test.inclusions", "**/*test*/**"
            property "sonar.import_unknown_files", true

            property "sonar.java.binaries", "${buildDir}/intermediates/javac/${android.testBuildType}/compile${android.testBuildType.capitalize()}JavaWithJavac/classes,${buildDir}/tmp/kotlin-classes/${android.testBuildType}UnitTest"
            property "sonar.java.source", "${getProjectDir()}/src/main/java"
            property "sonar.java.coveragePlugin", "jacoco"
            property "sonar.coverage.jacoco.xmlReportPaths", fileTree(dir: "${rootProject.buildDir}/reports/jacoco/", includes: ["**/createGlobalSkeletoidUnitTestReport.xml"])
            property "sonar.jacoco.itReportPath", fileTree(dir: "${buildDir}/outputs/code-coverage/connected/", includes: ["**/*.ec"])
            property "sonar.jacoco.reportPaths", fileTree(dir: "${buildDir}/jacoco/", includes: ["**/*.exec"])
            property "sonar.android.lint.report", "${buildDir}/outputs/lint-results.xml"
        }
    }
}
