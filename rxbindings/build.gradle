apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: "jacoco"

android {
    compileSdkVersion rootProject.compileSdkVersion
    buildToolsVersion rootProject.buildToolsVersion

    testBuildType "qa"

    defaultConfig {
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'proguard-rules.pro'
    }

    buildTypes {
        debug {
        }

        qa {
            matchingFallbacks = ['debug']
            testCoverageEnabled = true
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    packagingOptions {
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
    }

    lintOptions {
        // set to true to turn off analysis progress reporting by lint
        quiet true

        // if true, stop the gradle build if errors are found
        abortOnError false

        // do not ignore warnings
        warningsAsErrors true
    }
}

dependencies {

    api project(path: ':rxjava')

    implementation "com.jakewharton.rxbinding3:rxbinding:$rxBindingsVersion"
    implementation "com.jakewharton.rxbinding3:rxbinding-core:$rxBindingsVersion"
    implementation "com.jakewharton.rxbinding3:rxbinding-appcompat:$rxBindingsVersion"
    implementation "com.jakewharton.rxbinding3:rxbinding-drawerlayout:$rxBindingsVersion"
    implementation "com.jakewharton.rxbinding3:rxbinding-leanback:$rxBindingsVersion"
    implementation "com.jakewharton.rxbinding3:rxbinding-recyclerview:$rxBindingsVersion"
    implementation "com.jakewharton.rxbinding3:rxbinding-slidingpanelayout:$rxBindingsVersion"
    implementation "com.jakewharton.rxbinding3:rxbinding-swiperefreshlayout:$rxBindingsVersion"
    implementation "com.jakewharton.rxbinding3:rxbinding-viewpager:$rxBindingsVersion"
    implementation "com.jakewharton.rxbinding3:rxbinding-material:$rxBindingsVersion"

    testImplementation project(path: ':base', configuration: 'childTestDependencies')
}

task createUnitTestReport(type: JacocoReport, dependsOn: [':rxbindings:testQaUnitTest']) {
    group = "Reporting"
    description = "Generate Jacoco coverage reports after running tests."

    reports {
        html.enabled = true
        xml.enabled = true
    }

    def fileFilter = [
        "**/searchview/support/**",
        "**/searchview/SearchViewFocusChangeObservable*.*",
        "**/EditText*.*",
        "**/View*.*"
    ]

    def mainSrc = "$projectDir/src/main/java"
    def qaTree = fileTree(dir: "$buildDir/tmp/kotlin-classes/qa", excludes: fileFilter)

    getSourceDirectories().from(files([mainSrc]))
    getClassDirectories().from(files([qaTree]))
    getExecutionData().from(files("$buildDir/jacoco/testQaUnitTest.exec"))
}
