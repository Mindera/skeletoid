apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'jacoco'

android {
    compileSdkVersion rootProject.compileSdkVersion

    testBuildType "qa"

    defaultConfig {
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion

        consumerProguardFiles 'proguard-rules.pro'
    }

    buildTypes {
        debug {
        }

        qa {
            matchingFallbacks = ['debug']
            testCoverageEnabled = true
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    packagingOptions {
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
    }

    lintOptions {
        // set to true to turn off analysis progress reporting by lint
        quiet true

        // if true, stop the gradle build if errors are found
        abortOnError false

        // do not ignore warnings
        warningsAsErrors true
    }

    configurations.all {
        resolutionStrategy {
            resolutionStrategy.force "com.android.support:support-annotations:$supportLibraryVersion"
            // annotation processor conflicts
            resolutionStrategy.force "org.jetbrains.kotlin:kotlin-stdlib:$kotlinStdlibVersion"
            // lint conflicts
            resolutionStrategy.force "org.apache.httpcomponents:httpcore:$httpComponentsVersion"
            resolutionStrategy.force "com.google.guava:guava:$guavaVersion"
            // powermock
            resolutionStrategy.force "org.objenesis:objenesis:2.5"
            // fail eagerly on version conflict (includes transitive dependencies)
            // e.g. multiple different versions of the same dependency (group and name are equal)
            failOnVersionConflict()
        }
    }
}

dependencies {

    //Support
    implementation "com.android.support:appcompat-v7:$supportLibraryVersion"

    // RxJava
    implementation "io.reactivex.rxjava2:rxjava:$rxJavaVersion"
    implementation("io.reactivex.rxjava2:rxandroid:$rxAndroidVersion") {
        exclude group: 'io.reactivex.rxjava2', module: 'rxjava'
    }
    implementation("com.jakewharton.rxbinding2:rxbinding:$rxBindingsVersion") {
        exclude group: 'io.reactivex.rxjava2', module: 'rxjava'
        exclude group: 'io.reactivex.rxjava2', module: 'rxandroid'
        exclude group: 'com.android.support', module: 'support-annotations'
    }

    // RxBindings
    implementation("com.jakewharton.rxbinding2:rxbinding-appcompat-v7:$rxBindingsVersion") {
        exclude group: 'io.reactivex.rxjava2', module: 'rxjava'
        exclude group: 'io.reactivex.rxjava2', module: 'rxandroid'
        exclude group: 'com.android.support', module: 'appcompat-v7'
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'android.arch.lifecycle', module: 'viewmodel'
        exclude group: 'org.jetbrains.kotlin', module: 'kotlin-stdlib'
    }
    implementation("com.jakewharton.rxbinding2:rxbinding-recyclerview-v7:$rxBindingsVersion") {
        exclude group: 'com.android.support', module: 'recyclerview-v7'
        exclude group: 'io.reactivex.rxjava2', module: 'rxjava'
        exclude group: 'io.reactivex.rxjava2', module: 'rxandroid'
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'android.arch.lifecycle', module: 'runtime'
    }
    implementation("com.jakewharton.rxbinding2:rxbinding-support-v4:$rxBindingsVersion") {
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.android.support', module: 'support-core-ui'
        exclude group: 'io.reactivex.rxjava2', module: 'rxjava'
        exclude group: 'io.reactivex.rxjava2', module: 'rxandroid'
    }
    implementation("com.jakewharton.rxbinding2:rxbinding-design:$rxBindingsVersion") {
        exclude group: 'com.android.support', module: 'design'
        exclude group: 'io.reactivex.rxjava2', module: 'rxjava'
        exclude group: 'io.reactivex.rxjava2', module: 'rxandroid'
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.android.support', module: 'appcompat-v7'
        exclude group: 'com.android.support', module: 'recyclerview-v7'
        exclude group: 'android.arch.lifecycle', module: 'livedata-core'
        exclude group: 'android.arch.lifecycle', module: 'viewmodel'
        exclude group: 'android.arch.lifecycle', module: 'runtime'
    }
    implementation("com.jakewharton.rxbinding2:rxbinding-appcompat-v7-kotlin:$rxBindingsVersion") {
        exclude group: 'com.android.support', module: 'appcompat-v7'
        exclude group: 'com.jakewharton.rxbinding2', module: 'rxbinding-appcompat-v7'
        exclude group: 'io.reactivex.rxjava2', module: 'rxjava'
        exclude group: 'io.reactivex.rxjava2', module: 'rxandroid'
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'org.jetbrains.kotlin', module: 'kotlin-stdlib'
    }
    implementation("com.jakewharton.rxbinding2:rxbinding-recyclerview-v7-kotlin:$rxBindingsVersion") {
        exclude group: 'com.jakewharton.rxbinding2', module: 'rxbinding-recyclerview-v7'
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'org.jetbrains.kotlin', module: 'kotlin-stdlib'
        exclude group: 'io.reactivex.rxjava2', module: 'rxjava'
        exclude group: 'io.reactivex.rxjava2', module: 'rxandroid'
        exclude group: 'com.android.support', module: 'recyclerview-v7'
        exclude group: 'android.arch.lifecycle', module: 'runtime'
    }
    implementation("com.jakewharton.rxbinding2:rxbinding-support-v4-kotlin:$rxBindingsVersion") {
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.android.support', module: 'support-core-ui'
        exclude group: 'org.jetbrains.kotlin', module: 'kotlin-stdlib'
        exclude group: 'io.reactivex.rxjava2', module: 'rxjava'
        exclude group: 'io.reactivex.rxjava2', module: 'rxandroid'
    }
    implementation("com.jakewharton.rxbinding2:rxbinding-design-kotlin:$rxBindingsVersion") {
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'org.jetbrains.kotlin', module: 'kotlin-stdlib'
        exclude group: 'io.reactivex.rxjava2', module: 'rxjava'
        exclude group: 'io.reactivex.rxjava2', module: 'rxandroid'
        exclude group: 'com.jakewharton.rxbinding2', module: 'rxbinding-design'
    }

    // jakewharton'' libs use version 27.0.2 and we use 27.1.1, so we need to remove those.
    // we also need to guarantee that any project using this lib will not crash due to the lack
    // of these libs, so we should reference them here. But our project will not crash even
    // without these
    implementation("com.android.support:recyclerview-v7:$supportLibraryVersion")
    implementation("com.android.support:design:$supportLibraryVersion")

    testImplementation "junit:junit:$rootProject.junitVersion"
    testImplementation "org.mockito:mockito-core:$rootProject.mockitoVersion"
    testImplementation "org.powermock:powermock-module-junit4:$rootProject.powermockVersion"
    testImplementation "org.powermock:powermock-module-junit4-rule:$rootProject.powermockVersion"
    testImplementation "org.powermock:powermock-api-mockito2:$rootProject.powermockVersion"
    testImplementation "org.powermock:powermock-classloading-xstream:$rootProject.powermockVersion"
    testImplementation("org.robolectric:robolectric:$rootProject.robolectricVersion") {
        exclude group: 'com.thoughtworks.xstream', module: 'xstream'
        exclude group: 'org.codehaus.plexus', module: 'plexus-utils'
    }
}

jacoco {
    toolVersion "0.8.1"
}

//Unit Test Coverage filtered - Note: This is currently NOT being used on the Codacy report. This is to run locally if needed
task createSkeletoidUnitTestReport(type: JacocoReport, dependsOn: ['testQaUnitTest']) {

    reports {
        html.enabled = true
    }

    def fileFilter = [
            //Android stuff
            '**/R.class',
            '**/BR.class',
            '**/R$*.class',
            '**/BR$*.class',
            '**/BuildConfig.*',
            'android/**/*.*',
            '**/Manifest*.*',
            //Data Binding
            '**/*databinding/**/*.*',
            //Test
            '**/**Test*.*']

    //To support Java coverage on Unit tests
    def debugTree = fileTree(dir: "${buildDir}/intermediates/javac/qa/compileQaJavaWithJavac/classes", excludes: fileFilter)
    //To support Kotlin coverage on Unit tests
    def kotlinDebugTree = fileTree(dir: "${buildDir}/tmp/kotlin-classes/qaUnitTest", excludes: fileFilter)

    def mainSrc = "${project.projectDir}/src/main/java"
    def debugSrc = "${project.projectDir}/src/debug/java"

    sourceDirectories = files([mainSrc, debugSrc])


    classDirectories = files([debugTree], [kotlinDebugTree])
    executionData = files("${buildDir}/jacoco/testQaUnitTest.exec")
}
