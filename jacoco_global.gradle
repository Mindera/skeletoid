apply plugin: 'jacoco'

jacoco {
    toolVersion "0.8.5"
}

// https://github.com/gradle/gradle/issues/5184
tasks.withType(Test) {
    jacoco.includeNoLocationClasses = true
    jacoco.excludes = ['jdk.internal.*']
}

rootProject.afterEvaluate {
    def testCoverageBuildType = rootProject.getProperties().get('testBuildType') ?: 'qa'
    def testTaskNames = []
    def fileTrees = []
    def coverageSourceDirs = []
    def executionDataDirs = []

    //Remember to change in sonarqube build.gradle config
    def fileFilter = [
            //Android stuff
            '**/R.class',
            '**/BR.class',
            '**/R$*.class',
            '**/BR$*.class',
            '**/BuildConfig.*',
            'android/**/*.*',
            '**/Manifest*.*',
            //Data Binding
            '**/*databinding/**/*.*',
            //Test
            '**/**Test*.*',
            //Interfaces
            '**/interfaces/*.*',
            //UI Related items that make no sense to add unit tests to
            '**/views/*.*',
            '**/dialogs/*.*',
            '**/animation/*.*',
            '**/UnfreezeScreenActivity.*',
            //RxJava that also make no sense to test
            '**/Schedulers.*',
            //No logic utils
            '**/utils/http/HttpCodes.*'
    ]

    rootProject.subprojects.collect() { subproject ->
        if (isAndroidProject(subproject)) {
            subproject.android {
                testOptions {
                    unitTests.all {
                        jacoco {
                            includeNoLocationClasses = true
                        }
                    }
                }
            }
        }

        subproject.afterEvaluate {

            testTaskNames.add(":${subproject.name}:test${testCoverageBuildType.capitalize()}UnitTest")

            def javaFileTree = fileTree(
                    dir: "${subproject.buildDir}/intermediates/javac/${testCoverageBuildType}/classes",
                    excludes: fileFilter
            )
            fileTrees.add(javaFileTree)

            def kotlinFileTree = fileTree(
                    dir: "${subproject.buildDir}/tmp/kotlin-classes/${testCoverageBuildType}",
                    excludes: fileFilter
            )
            fileTrees.add(kotlinFileTree)

            def coverageSourceDir = [
                    "${subproject.projectDir}/src/main/java",
                    "${subproject.projectDir}/src/${testCoverageBuildType}/java"
            ]
            coverageSourceDirs.add(coverageSourceDir)

            def executionDataDir = "${subproject.buildDir}/jacoco/test${testCoverageBuildType.capitalize()}UnitTest.exec"
            executionDataDirs.add(executionDataDir)
        }
    }

    //'qa' is a default test build type. To change run:
    //gradlew createGlobalSkeletoidUnitTestReport -PtestBuildType={yourTestBuildType}
    task createGlobalSkeletoidUnitTestReport(type: JacocoReport, dependsOn: testTaskNames) {
        group = "Reporting"
        description = "Generate Jacoco Global coverage report"

        getClassDirectories().from(files(fileTrees))
        getAdditionalSourceDirs().from(files(coverageSourceDirs))
        getSourceDirectories().from(files(coverageSourceDirs))
        getExecutionData().from(files(executionDataDirs))

        reports {
            xml.enabled = true
            html.enabled = true
        }
    }
}

protected static boolean isAndroidProject(final Project project) {
    final boolean isAndroidLibrary = project.plugins.hasPlugin('com.android.library')
    final boolean isAndroidApp = project.plugins.hasPlugin('com.android.application')
    return isAndroidLibrary || isAndroidApp
}
